@go.Package { name = "github.com/kdeps/schema/gen/resource" }

module org.kdeps.pkl.Resource

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.5.0#/go.pkl"

import "Project.pkl"
import "APIServer.pkl"
import "APIServerResponse.pkl"
import "LLM.pkl"
import "ENV.pkl"
import "Tag.pkl"

hidden nameStringRegex = Regex(#"(^\w+$)"#)
hidden actionStringRegex = Regex(#"^(\w+|@\w+(/[\w-]+)(:[\w.]+)?)$"#)
hidden isValidId = (str) ->
  if (str.matches(actionStringRegex))
    true
  else
    throw("Error: Invalid name: The name contains invalid characters. Please ensure it only includes alphanumeric characters (letters and numbers) and is not empty.")

hidden isValidName = (str) ->
  if (str.matches(nameStringRegex))
    true
  else
    throw("Error: Invalid category name: The name contains invalid characters. Please ensure it only includes alphanumeric characters (letters and numbers) and is not empty.")

hidden isValidDependency = (str) ->
  if (str.matches(actionStringRegex))
    true
  else
    throw("Action must be either a simple alphanumeric string or start with `@`, followed by `/action` and an optional `:version` (e.g., `@agent/action:1.0.0`).")

id: String(isValidId)
name: String
description: String
category: String(isValidName)
requires: Listing<String(isValidDependency)>?
action: Listing<ResourceAction>

class ResourceAction {
  name: String
  exec: String?
  env: Listing<ENV.ResourceEnv>?
  tags: Listing<Tag.ResourceTag>?
  chat: Listing<LLM.ResourceChat>?
  skipCondition: Listing<Boolean>?
  preflightCheck: Listing<Boolean>?
  postflightCheck: Listing<Boolean>?
  httpClient: Listing<APIServer.ResourceHTTPClient>?
  apiResponse: APIServerResponse?
}
