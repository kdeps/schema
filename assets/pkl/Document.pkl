/// Common parser and document renderer functions used across all resources.
///
/// Tools for Parsing and Generating JSON, YAML and XML documents
@ModuleInfo { minPklVersion = "0.28.2" }

@go.Package { name = "github.com/kdeps/schema/gen/document" }

open module org.kdeps.pkl.Document

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.10.0#/go.pkl"

import "pkl:json"
import "pkl:test"
import "pkl:math"
import "pkl:platform"
import "pkl:semver"
import "pkl:shell"
import "pkl:xml"
import "pkl:yaml"

/// Parse JSON data using the JSON parser with list-based mapping.
///
/// If the data cannot be parsed as JSON, returns the raw string data.
/// [data]: The JSON string to parse.
/// Returns the parsed JSON data or the original string if parsing fails.
function jsonParser(data: String?) =
  if (data != null)
    if (test.catchOrNull(() -> (new json.Parser { useMapping = true }).parse(data)) != null)
      (new json.Parser { useMapping = true }).parse(data)
    else if (test.catchOrNull(() -> (new json.Parser { useMapping = false }).parse(data)) != null)
      (new json.Parser { useMapping = false }).parse(data)
    else
      data
  else
    ""

/// Parse JSON data using the JSON parser with object-based mapping.
///
/// [data]: The JSON string to parse.
/// Returns the parsed JSON data as an object structure.
function jsonParserMapping(data: String?) =
  if (data != null)
    if (test.catchOrNull(() -> (new json.Parser { useMapping = true }).parse(data)) != null)
      (new json.Parser { useMapping = true }).parse(data)
    else
      data
  else
    ""

/// Renders a JSON document.
///
/// [value]: The value to render as a JSON document.
/// Returns the JSON representation of the value, or the original value if rendering fails.
function jsonRenderDocument(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new JsonRenderer {}).renderDocument(value)))
    if (result != null) result else value
  else
    ""

/// Renders a JSON value.
///
/// [value]: The value to render as JSON.
/// Returns the JSON representation of the value, or the original value if rendering fails.
function jsonRenderValue(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new JsonRenderer {}).renderValue(value)))
    if (result != null) result else value
  else
    ""

/// Renders a YAML document.
///
/// [value]: The value to render as a YAML document.
/// Returns the YAML representation of the value, or the original value if rendering fails.
function yamlRenderDocument(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new YamlRenderer {}).renderDocument(value)))
    if (result != null) result else value
  else
    ""

/// Renders a YAML value.
///
/// [value]: The value to render as YAML.
/// Returns the YAML representation of the value, or the original value if rendering fails.
function yamlRenderValue(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new YamlRenderer {}).renderValue(value)))
    if (result != null) result else value
  else
    ""

/// Renders an XML document.
///
/// [value]: The value to render as an XML document.
/// Returns the XML representation of the value, or the original value if rendering fails.
function xmlRenderDocument(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new PListRenderer {}).renderDocument(value)))
    if (result != null) result else value
  else
    ""

/// Renders an XML value.
///
/// [value]: The value to render as XML.
/// Returns the XML representation of the value, or the original value if rendering fails.
function xmlRenderValue(value: Any?) =
  if (value != null)
    let (result = test.catchOrNull(() -> (new PListRenderer {}).renderValue(value)))
    if (result != null) result else value
  else
    ""
